plugins {
    id 'java-gradle-plugin'
    id 'org.jetbrains.kotlin.jvm'
    id 'io.gitlab.arturbosch.detekt'
    id 'net.meilcli.hubber.toolchain.dependencies'
}

import net.meilcli.hubber.config.Dependencies

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(Dependencies.OrgJetbrainsKotlin.KotlinBom)
    implementation Dependencies.OrgJetbrainsKotlin.KotlinStdlibJdk8
    implementation Dependencies.OrgJetbrainsDokka.DokkaGradlePlugin
    implementation Dependencies.IoGitlabArturboschDetekt.DetektGradlePlugin

    detektPlugins Dependencies.IoGitlabArturboschDetekt.DetektFormatting
}

hubberDependencies {
    includeSourceSets = true
}

gradlePlugin {
    plugins {
        detekt {
            id = 'net.meilcli.hubber.toolchain.config.detekt'
            implementationClass = 'net.meilcli.hubber.toolchain.config.core.DetektPlugin'
        }
        dokka {
            id = 'net.meilcli.hubber.toolchain.config.dokka'
            implementationClass = 'net.meilcli.hubber.toolchain.config.core.DokkaPlugin'
        }
    }
}

def findDetektYmlDirectory() {
    def current = buildDir
    while (current.parentFile != null) {
        if (new File(current, "detekt.yml").exists()) {
            return current
        }
        current = current.parentFile
    }
    return current
}

detekt {
    buildUponDefaultConfig = true
    config = files("${findDetektYmlDirectory()}/detekt.yml")
    basePath = findDetektYmlDirectory()
}
